// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Not16(in=x, out=negx);
    Not16(in=y, out=negy);
    Mux4Way16(a=x, b[0..15]=false, c=negx, d[0..15]=true, sel[0]=zx, sel[1]=nx, out=w0);
    Mux4Way16(a=y, b[0..15]=false, c=negy, d[0..15]=true, sel[0]=zy, sel[1]=ny, out=w1);

    And16(a=w0, b=w1, out=w2);
    Add16(a=w0, b=w1, out=w3);
    Not16(in=w2, out=w4);
    Not16(in=w3, out=w5);
    // out[16]
    Mux4Way16(a=w2, b=w3, c=w4, d=w5, sel[0]=f, sel[1]=no, out=out, out[0..7]=w6, out[8..15]=w7, out[15]=w8);
    // zr
    Or8Way(in=w6, out=w9);
    Or8Way(in=w7, out=w10);
    Or(a=w9, b=w10, out=w11);
    Not(in=w11, out=zr);
    // ng
    And(a=w8, b=true, out=ng);
}